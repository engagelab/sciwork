<?xml version="1.0" encoding="utf-8"?>
<s:BorderContainer xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" contentBackgroundColor="#EEEEEE" borderAlpha=".3" borderWeight="3">
	
	<fx:Declarations>
		<fx:String id="groupId" />
		<fx:String id="taskId" />
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import com.adobe.serialization.json.JSON;
			import com.jonas.net.Multipart;
			import com.pfp.events.JPEGAsyncCompleteEvent;
			import com.pfp.utils.JPEGAsyncEncoder;
			import com.seesmic.as3.xmpp.XMPP;
			
			import components.Tweet;
			
			import mx.controls.Alert;
			import mx.controls.ProgressBar;
			import mx.events.FlexEvent;
			import mx.utils.UIDUtil;
			
			private var asyncEncoder:JPEGAsyncEncoder;
			
			private var _tweetsForGroup:Array;
			private var _conn:XMPP;
			private var _energyID:String;
			
			private var groupReady:Boolean = false;
			
			public function get conn():XMPP
			{
				return _conn;
			}

			public function get tweetsForGroup():Array
			{
				return _tweetsForGroup;
			}
			
			/**
			 * 
			 * function to display all tweets
			 * 
			 **/
			public function populateTweets(tweetsForGroup:Array, groupId:String, taskId:String, conn:XMPP, energyId:String):void {
				this._tweetsForGroup = tweetsForGroup;
				this._conn = conn;
				this.groupId = groupId;
				this.taskId = taskId;
				this._energyID = energyId;
				
				refreshTweetsDisplay();
			}
			
			/**
			 * 
			 * function to add all tweets to their container
			 * 
			 **/
			private function refreshTweetsDisplay():void {
				if(groupReady) {
					
					//refresh the view
					tweetsContainer.removeAllElements();
					
					//build the correct arrays
					var tweet:Tweet;
					var tweetObject:Object;
					for each (var twt:Object in tweetsForGroup) {
						tweet = new Tweet();
						tweet.data = twt;
						tweet.conn = conn;
						
						//need to see if tweet is visisble
						if(twt.isVisible) {
							//place the tweets in the view
							tweetsContainer.addElement(tweet);
						}
					}
				}
			}
			
			
			/**
			 * 
			 * function to save the groupcontainer as a picture
			 * 
			 **/
			protected function saveTweetAreaPicture(event:MouseEvent):void {
				this.currentState = "encoding";
				var compositionBitmapData:BitmapData = new BitmapData(tweetsContainer.width,tweetsContainer.height);
				compositionBitmapData.draw(tweetsContainer);
				
				asyncEncoder = new JPEGAsyncEncoder(100);
				asyncEncoder.PixelsPerIteration = 128;
				asyncEncoder.addEventListener(ProgressEvent.PROGRESS, updateProgress);
				asyncEncoder.addEventListener(JPEGAsyncCompleteEvent.JPEGASYNC_COMPLETE, asyncComplete);
				asyncEncoder.encode(compositionBitmapData);
			}
			
			/**
			 * 
			 * function to handle encoding progress event
			 * 
			 **/
			private function updateProgress(event:ProgressEvent):void {
				pbar.setProgress(event.bytesLoaded, event.bytesTotal);
			}
			
			/**
			 * 
			 * function to handle image posting when encoding progress is complete
			 * 
			 **/
			private function asyncComplete(event:JPEGAsyncCompleteEvent):void {
				var form:Multipart = new Multipart(resourceManager.getString('resources', 'SERVER_URL')+"/image/"+groupId+"/"+taskId+"/3");
				form.addFile("picture", event.ImageData, "application/octet-stream", UIDUtil.createUID()+".jpg", true);
				
				var loader:URLLoader = new URLLoader();
				loader.addEventListener(Event.COMPLETE, onComplete);
				try {
					loader.load(form.request);
				} catch (error: Error) {
					Alert.show("Unable to load requested document", "image upload error");
				}
			}
			
			/**
			 * 
			 * function called when image posting is complete
			 * 
			 **/
			protected function onComplete (e: Event):void {
				this.currentState = "default";
				dispatchEvent(new Event(resourceManager.getString('resources', 'REFRESH_TWEETS_PORTFOLIO'), true));
			}
			
			/**
			 * 
			 * function called when group container for tweets is ready. Needed in order to create tweets based on parent' size.
			 * 
			 **/
			protected function groupComplete(event:FlexEvent):void {
				groupReady = true;
				refreshTweetsDisplay();
			}
			
		]]>
	</fx:Script>
	
	<s:states>
		<s:State name="default" />
		<s:State name="encoding" />
	</s:states>
	
	<s:Button top="0" right="0" label="Save" click="saveTweetAreaPicture(event)" />
	<s:Group width="100%" height="100%" id="tweetsContainer" creationComplete="groupComplete(event)" />
	
	<s:SkinnableContainer width="100%" height="100%" backgroundAlpha=".8" backgroundColor="#333333" includeIn="encoding">
		<mx:ProgressBar id="pbar" horizontalCenter="0" verticalCenter="0" mode="manual" color="#ffffff"/>
	</s:SkinnableContainer>
</s:BorderContainer>
