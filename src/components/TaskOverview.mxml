<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" width="400" height="300" creationComplete="init(event)">
	<fx:Declarations>
		<s:HTTPService method="GET" result="getSceneContributions_resultHandler(event)" fault="getSceneContributions_faultHandler(event)" resultFormat="text" id="getSceneContributions" showBusyCursor="true" />
		<s:HTTPService method="GET" result="getTaskPortfolioComment_resultHandler(event)" fault="getTaskPortfolioComment_faultHandler(event)" resultFormat="text" id="getTaskPortfolioComment" showBusyCursor="true" />
		<s:HTTPService url="{resourceManager.getString('resources', 'SERVER_URL')+'/groupInfo'}" method="GET" result="getAllGroups_resultHandler(event)" fault="getAllGroups_faultHandler(event)" resultFormat="text" id="getAllGroups" />
		<fx:Object id="currentGroup" />
		<fx:Object id="currentTask" />
	</fx:Declarations>
	
	<fx:Script>
		<![CDATA[
			import com.adobe.serialization.json.JSON;
			
			import content.workarea.materials.MaterialsItem;
			
			import flash.text.engine.TabAlignment;
			
			import listeners.MaterialToPreviewEvent;
			import listeners.ShowFullscreenEvent;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.FlexEvent;
			import mx.rpc.events.FaultEvent;
			import mx.rpc.events.ResultEvent;
			
			import spark.collections.Sort;
			import spark.collections.SortField;
			import spark.events.IndexChangeEvent;
			
			[Bindable] private var _groupNameList:ArrayCollection;
			[Bindable] private var _groupList:ArrayCollection;
			private var groupsLoaded:Boolean = false;
			public static const VIDEO_TYPE:String = "VIDEO_TYPE";
			public static const IMAGE_TYPE:String = "IMAGE_TYPE";
			public static const PORTFOLIO_TYPE:String = "PORTFOLIO";

			private var sciApp:sciwork = mx.core.FlexGlobals.topLevelApplication as sciwork;
			
			// Data includes URL to album cover.
/*			[Bindable]
			private var actualCollection:ArrayCollection = new ArrayCollection();
			[Bindable]
			private var sampleCollection:ArrayCollection = new ArrayCollection([
				{Task:'Question 1', Complete:'Complete', Images:["assets/simu/pics/crossBG.png"]},
				{Task:'Question 2', Complete:'Incomplete', Images:["assets/simu/pics/stop.png", "assets/simu/pics/start.png"]},
				{Task:'Question 1', Complete:'Complete', Images:["assets/fs.png"]},
				{Task:'Question 2', Complete:'Complete', Images:["assets/7.png"]},
				{Task:'Question 3', Complete:'Incomplete', Images:["assets/trash.png"]}
			]);
*/			
			private function init(event:FlexEvent):void {
				this.addEventListener(resourceManager.getString('resources', 'MATERIAL_TO_PREVIEW'), handleMaterialFullscreenRequest);
				getAllGroups.send();
			}
			public function setTask(task:Object):void {
				currentTask = task;
				if(groupsLoaded)
					refreshData(groupSelector.selectedIndex);
			}
			private function changeHandlerProgrammatic(event:FlexEvent):void {
				if(groupsLoaded)
					refreshData(groupSelector.selectedIndex);
			} 
			private function refreshData(selectedGroupIndex:int):void {
				materialItemsT1.removeAllElements();
				materialItemsT2.removeAllElements();
				materialItemsT3.removeAllElements();
				comment.text = "";
				
				var sceneArray:ArrayCollection = sciApp.tabBar.sceneBar.dataProvider as ArrayCollection;
				var taskArray:ArrayCollection = sceneArray.getItemAt(sciApp.tabBar.taskBar.selectedIndex + 2).tasks as ArrayCollection;
				
				task1.text = task2.text = task3.text = "";
				for(var taskNumber:int = 1; taskNumber <= taskArray.length; taskNumber++) {
					this["task"+taskNumber].text = taskArray[taskNumber-1].label;
				}
				
				getSceneContributions.url = resourceManager.getString('resources', 'SERVER_URL') + "/sceneContributions/" + _groupList[selectedGroupIndex].id + "/" + currentTask.portFolioSceneId;
				getSceneContributions.send();
				
				getTaskPortfolioComment.url = resourceManager.getString('resources', 'SERVER_URL') + "/finalPortfolioTaskComment/" + _groupList[selectedGroupIndex].id + "/" + currentTask.id;
				getTaskPortfolioComment.send();
			}
			
			/**
			 *
			 * Result handling method for getSceneContributions service 
			 * 
			 **/
			protected function getSceneContributions_resultHandler(event:ResultEvent):void {
				//store the returned object as JSON 
				var contributions:Object = com.adobe.serialization.json.JSON.decode(String(event.result));
				
				parseMaterialSubArray(contributions.svideos, VIDEO_TYPE);
				parseMaterialSubArray(contributions.simages, IMAGE_TYPE);
			}
			/**
			 *
			 * Parsing method to assign retrieved material to a correct preview item
			 * 
			 **/
			protected function parseMaterialSubArray(subArray:Array, materialType:String):void {
				var newItem:MaterialsItem;
				var materialGroup:int;
				
				var sceneArray:ArrayCollection = sciApp.tabBar.sceneBar.dataProvider as ArrayCollection;
				var taskArray:ArrayCollection = sceneArray.getItemAt(sciApp.tabBar.taskBar.selectedIndex + 2).tasks as ArrayCollection;

				//extract materials
				for each (var material:Object in subArray) {
					var newTaskId:String = material.taskId;
					
					for(var i:int = 0; i<taskArray.length; i++) {
						if(taskArray[i].taskId == material.taskId)
							materialGroup = i+1;
					}
					
					if(material.isFinalPortfolio) {
						newItem = new MaterialsItem();
						newItem.sourceType = PORTFOLIO_TYPE;
						newItem.isFinalPortfolio = material.isFinalPortfolio;
						newItem.isPortfolio = material.isPortfolio;
						newItem.xpos = material.xpos;
						newItem.ypos = material.ypos;
						newItem.scaleX = 1.2;
						newItem.scaleY = 1.2;
						newItem.deleter.visible = false;
						newItem.deleter.includeInLayout = false;
						
						this["materialItemsT"+materialGroup].addElement(newItem);
						
						//assign id
						newItem.id = material.id;
						
						//pick correct preview type
						switch(materialType) {
							case VIDEO_TYPE:
								newItem.setupPreview(VIDEO_TYPE, material.uri, resourceManager.getString('resources', 'YOUTUBE_BASE_IMAGE_URL')+material.uri+"/1.jpg");
								break;
							case IMAGE_TYPE:
								newItem.setupPreview(IMAGE_TYPE, resourceManager.getString('resources', 'SERVER_URL')+material.filePath, resourceManager.getString('resources', 'SERVER_URL')+material.filePath);
								break;
						}
					}
				}
			}
			/**
			 *
			 * Result handling method for getAllGroups service 
			 * 
			 **/
			protected function getAllGroups_resultHandler(event:ResultEvent):void {
				//store the returned object as JSON
				var res:Object = com.adobe.serialization.json.JSON.decode(String(event.result));
				
				//instantiate and build group array
				_groupList = new ArrayCollection();
				for each (var act:Object in res) {
					// Exclude the teacher from this listing
					if(act.id != "506c21c742060774910aeb8b")
						_groupList.addItem({label: act.name, id: act.id, color: act.colour});
				}
				
				//ordering the list alphabetically
				var sortfield:SortField = new SortField();
				sortfield.name = "title";
				sortfield.descending = false;
				
				var sort:Sort = new Sort();
				sort.fields = [sortfield];
				
				_groupList.sort = sort;
				_groupList.refresh();
				
				groupSelector.selectedIndex = 0;
				groupsLoaded = true;
			//	refreshData(0);
			}
			/**
			 *
			 * Error handling method for getAllGroups service 
			 * 
			 **/
			protected function getAllGroups_faultHandler(event:FaultEvent):void {
				Alert.show("An error occured while retrieving group names from the server.", "Group service error");
			}
			/**
			 * 
			 * Error handling method for getSceneContributions service
			 * 
			 **/
			protected function getSceneContributions_faultHandler(event:FaultEvent):void {
				Alert.show("An error occured while retrieving the scenecontributions assets from the server.", "SceneContribution service error");
			}
			
			/**
			 * 
			 * Error handling method for getTaskPortfolioComment service
			 * 
			 **/
			protected function getTaskPortfolioComment_faultHandler(event:FaultEvent):void {
				Alert.show("An error occured while retrieving the taskportfoliocomments from the server.", "TaskPortfolioComment service error");
			}
			
			/**
			 *
			 * Result handling method for getTaskPortfolioComment service 
			 * 
			 **/
			protected function getTaskPortfolioComment_resultHandler(event:ResultEvent):void {
				var comments:Array = com.adobe.serialization.json.JSON.decode(String(event.result));
				
				if(comments.length > 0)
					comment.text = comments[0].text;
			}
			/**
			 * 
			 * Event handling method for MATERIAL_TO_PREVIEW
			 * 
			 **/
			protected function handleMaterialFullscreenRequest(evt:MaterialToPreviewEvent):void {
				var stfe:ShowFullscreenEvent = new ShowFullscreenEvent(resourceManager.getString('resources', 'SHOW_FULLSCREEN'), true);
				stfe.shouldShow = true;
				stfe.mode = "picsvids";
				stfe.showPullOutMenu = false;
				stfe.material = evt.materialItem;
				dispatchEvent(stfe);
			}

		]]>
	</fx:Script> 
	
	<!--s:ButtonBar id="groupSelector" width="100%" labelField="title" dataProvider="_groupList" skinClass="skins.taskOverviewButtonBarSkin" buttonMode="true" requireSelection="true" /-->
	<s:TabBar id="groupSelector" width="{this.width}" dataProvider="{_groupList}" top="-21" skinClass="skins.groupTabBarSkin" valueCommit="changeHandlerProgrammatic(event)" requireSelection="true">
		<s:filters>
			<s:DropShadowFilter 
				distance="3" alpha=".3" blurX="7" blurY="7"
				angle="90" />
		</s:filters>
	</s:TabBar>
	<s:VGroup width="100%" height="100%" top="20" gap="15">
		<s:Group id="materials" width="100%" height="60%" >
			<s:layout>
				<s:VerticalLayout variableRowHeight="false" rowHeight="85"/> 
			</s:layout>
			<s:VGroup>
				<s:Rect id="backgroundRect" left="0" right="0" top="0" bottom="0">
					<s:fill>
						<s:LinearGradient rotation="90">
							<s:GradientEntry color="#EEEEEE" />
							<s:GradientEntry color="#FFFFFF" />
							<s:GradientEntry color="#FFFFFF" />
						</s:LinearGradient>
					</s:fill>
				</s:Rect>
				<s:Label id="task1" text="Task 1" />
				<s:Scroller top="5" right="5" bottom="5" left="5">
					<s:HGroup id="materialItemsT1" />
				</s:Scroller>
			</s:VGroup>
			<s:VGroup>
				<s:Rect id="backgroundRect2" left="0" right="0" top="0" bottom="0">
					<s:fill>
						<s:LinearGradient rotation="90">
							<s:GradientEntry color="#EEEEEE" />
							<s:GradientEntry color="#FFFFFF" />
							<s:GradientEntry color="#FFFFFF" />
						</s:LinearGradient>
					</s:fill>
				</s:Rect>
				<s:Label id="task2" text="Task 2" />
				<s:Scroller top="5" right="5" bottom="5" left="5">
					<s:HGroup id="materialItemsT2" />
				</s:Scroller>
			</s:VGroup>
			<s:VGroup>
				<s:Rect id="backgroundRect3" left="0" right="0" top="0" bottom="0">
					<s:fill>
						<s:LinearGradient rotation="90">
							<s:GradientEntry color="#EEEEEE" />
							<s:GradientEntry color="#FFFFFF" />
							<s:GradientEntry color="#FFFFFF" />
						</s:LinearGradient>
					</s:fill>
				</s:Rect>
				<s:Label id="task3" text="Task 3" />
				<s:Scroller top="5" right="5" bottom="5" left="5">
					<s:HGroup id="materialItemsT3"/>
				</s:Scroller>
			</s:VGroup>
		</s:Group>
		<s:VGroup width="100%">
			<s:Label text="Kommentere" />
			<s:TextArea id="comment" editable="false" width="100%" height="80%" borderAlpha=".3"/>
		</s:VGroup>
	</s:VGroup>
</s:Group>
