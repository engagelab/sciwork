<?xml version="1.0" encoding="utf-8"?>
<s:ItemRenderer xmlns:fx="http://ns.adobe.com/mxml/2009" 
				xmlns:s="library://ns.adobe.com/flex/spark" 
				xmlns:mx="library://ns.adobe.com/flex/mx" 
				autoDrawBackground="true" creationComplete="creationCompleteHandler(event)" width="150" minHeight="30" 
				mouseDown="mouseDownHandler(event)" mouseUp="mouseUpHandler(event)" mouseOver="mouseOverHandler(event)" mouseOut="mouseOutHandler(event)">
	
	<fx:Script>
		<![CDATA[
			import com.adobe.serialization.json.JSON;
			
			import listeners.TweetUpdaterEvent;
			
			import mx.events.FlexEvent;
			import mx.managers.CursorManager;
			
			private var oldX:Number;
			private var oldY:Number;
			
			[Bindable]
			[Embed(source="assets/drag.png")]
			private var DragIcon:Class;
			
			private var cursorId:int;
			
			/**
			 *
			 * Called when itemrenderer creation is complete
			 * 
			 **/
			protected function creationCompleteHandler(event:FlexEvent):void {
				itemBackground.color = uint(mx.core.FlexGlobals.topLevelApplication.currentGroup.colour);
			}
			
			/**
			 *
			 * Called when mouse is pressed on this item
			 * 
			 **/
			protected function mouseDownHandler(event:MouseEvent):void {
				//we log the old X and Y, in case the service cannot accept the move
				oldX = this.x;
				oldY = this.y;
				//start dragging
				this.startDrag(false, new Rectangle(0, 0, this.parent.width-this.width, this.parent.height-this.height));
			}
			
			/**
			 *
			 * Called when mouse is released on this item
			 * 
			 **/
			protected function mouseUpHandler(event:MouseEvent):void {
				//stop dragging
				this.stopDrag();
				//update the new position with the server
				
				var currentTweet:Object = data;
				currentTweet.xpos = this.x;
				currentTweet.ypos = this.y
				
				var tu:TweetUpdater = new TweetUpdater();
				tu.addEventListener(TweetUpdaterEvent.UPDATE_SUCCESSFUL, handleUpdateSuccessful);
				tu.addEventListener(TweetUpdaterEvent.UPDATE_FAILED, handleUpdateFailure);
				tu.postTweetToService(resourceManager.getString('resources', 'SERVER_URL')+"/tweet", currentTweet);
			}
			
			/**
			 *
			 * Called when the tweetUpdater is successful
			 * 
			 **/
			protected function handleUpdateSuccessful(evt:TweetUpdaterEvent):void {
				//it's all good
				var res:Object = com.adobe.serialization.json.JSON.decode(String(evt.result));
				this.x = res.xpos;
				this.y = res.ypos;
			}
			
			/**
			 *
			 * Called when the tweetUpdater fails
			 * 
			 **/
			protected function handleUpdateFailure(evt:TweetUpdaterEvent):void {
				//something went wrong with the service, reposition the item to its last known position
				this.x = oldX;
				this.y = oldY;
			}
			
			/**
			 *
			 * Called when mouse rolls over this item
			 * 
			 **/
			protected function mouseOverHandler(event:MouseEvent):void {
				cursorId = CursorManager.setCursor(DragIcon);
			}
			
			/**
			 *
			 * Called when rolls out of this item
			 * 
			 **/
			protected function mouseOutHandler(event:MouseEvent):void {
				CursorManager.removeCursor(cursorId);
			}
			
		]]>
	</fx:Script>
		
	<s:Rect left="0" right="0" top="0" bottom="0" radiusX="5" radiusY="5">
		<s:fill>
			<s:SolidColor id="itemBackground" />
		</s:fill>
		<s:stroke>
			<s:SolidColorStroke color="#666666" />
		</s:stroke>
	</s:Rect>
	<s:Label text="{data.text}" color="#FFFFFF" left="10" right="10" height="100%" verticalAlign="middle" textAlign="left"/>
	
</s:ItemRenderer>
